// SPDX-FileCopyrightText: Copyright 2025 Securosys SA
// SPDX-License-Identifier: Apache-2.0

apply plugin: 'base'
apply plugin: 'com.palantir.docker'

configurations {
	deployment
}

dependencies {
	deployment project(path: ':fireblocks-application', configuration: 'bootArchives')
}

task unpackBootJar (type: Copy) {
	dependsOn configurations.deployment
	from(zipTree(configurations.deployment.files[0]))
	into("build/dependency")
	doLast {
		println "Unzipping " + configurations.deployment.files[0]
	}
}

task prepareDockerContext(type: Copy) {
	dependsOn unpackBootJar
	into("build/docker")
	from("Dockerfile") { into(".")}
	from("build/dependency/BOOT-INF/lib") { into("install/BOOT-INF/lib") }
	from("build/dependency/META-INF") { into("install/META-INF") }
	from("build/dependency/BOOT-INF/classes") { into("install/BOOT-INF/classes") }
	doLast {
		println "Prepared Docker build context in 'build/docker/'"
	}
}

task dockerBuildAmd64(type: Exec) {
	dependsOn prepareDockerContext
	workingDir "build/docker"
	commandLine 'docker', 'buildx', 'build', '--platform', 'linux/amd64',
			'-t', "securosys-docker-build.securosys.com/fireblocks/external-key-store-application-amd64:${project.version}", "."
			//'--push', '.'
}

task dockerBuildArm64(type: Exec) {
	dependsOn prepareDockerContext
	workingDir "build/docker"
	commandLine 'docker', 'buildx', 'build', '--platform', 'linux/arm64',
			'-t', "securosys-docker-build.securosys.com/fireblocks/external-key-store-application-arm64:${project.version}", "."
			//'--push', '.'
}

buildscript {
	ext {
		repoUser = findProperty('repoUser') ?: System.env.BINREPO_USER
		repoPass = findProperty('repoPass') ?: System.env.BINREPO_TOKEN
	}
	repositories {
		maven {
			url 'https://binrepo.adnovum.ch/artifactory/adn-public-maven-vi'
			credentials {
				username = repoUser
				password = repoPass
			}
		}
	}
}